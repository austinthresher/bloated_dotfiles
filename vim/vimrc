source ~/.virc
function! g:Options()
    " Allow folding in files
    set foldenable
    " Allow files to customize settings on open
    set modeline
    " Characters used when list=on
    set listchars=eol:$,tab:>\ ,extends:>,precedes:<,nbsp:+,trail:_
    " Character used to mark wrapped lines
    set showbreak==>\
    " Search as you're typing
    set hlsearch
    " Enable the mouse
    set mouse=a
    " Set US English for spellcheck
    set spelllang=en_us
    " Don't wrap lines by default
    set nowrap
    " Don't page output
    set more
    " Don't try to set the cursor
    "set guicursor=
    " CursorHold kicks in after 1/10 second
    set updatetime=100
    " Reduce screen artifacts by delaying redraw
    set lazyredraw
    " Show cmd in progress
    set showcmd
    " Scroll options
    set sidescroll=1
    set scrolloff=0
    set sidescrolloff=1
    " Allow windows to collapse completely
    set winminwidth=1
    set winminheight=1
    set completeopt+=menuone,noinsert,preview,longest
    " Use line drawing char for vsp, spaces for folds
    if has('nvim')
        set fillchars=vert:â”‚,fold:\ ,eob:\ ,diff:-
    endif
endfunc
call g:Options()

function! g:Globals()
    try
        let g:lyra_string_bg = v:false
        let g:lyra_use_system_colors = v:false
        let g:lyra_transparent = v:false
	let g:lyra_no_highlighting = v:false
        colorscheme lyra
    endtry
    let g:cmake_build_dir = 'build'
    let g:ide = has('nvim')
    let g:nproc = 1
    try | let g:nproc = str2nr(system('nproc')) | endtry
    let g:python_highlight_all = v:true
endfunction
call g:Globals()

function! g:Plugins()
    if has('nvim')
        let g:helper_plug_path = $HOME.'/.config/nvim/repos'
    else
        let g:helper_plug_path = $HOME.'/.vim/repos'
    endif

    call helper#begin()

    call helper#plug('MarcWeber/vim-addon-mw-utils')
    call helper#plug('ekalinin/dockerfile.vim')
    call helper#plug('google/vim-searchindex')
    call helper#plug('guns/xterm-color-table.vim')
    call helper#plug('junegunn/gv.vim')
    call helper#plug('jvirtanen/vim-octave')
    call helper#plug('machakann/vim-sandwich')
    call helper#plug('mboughaba/i3config.vim')
    call helper#plug('michaeljsmith/vim-indent-object')
    call helper#plug('peterrincker/vim-argumentative')
    call helper#plug('romainl/vim-qf')
    call helper#plug('roxma/vim-paste-easy')
    call helper#plug('tbastos/vim-lua')
    call helper#plug('terryma/vim-multiple-cursors')
    call helper#plug('thinca/vim-ref')
    call helper#plug('tomtom/tlib_vim')
    call helper#plug('tpope/vim-abolish')
    call helper#plug('tpope/vim-apathy')
    call helper#plug('tpope/vim-commentary')
    call helper#plug('tpope/vim-eunuch')
    call helper#plug('tpope/vim-fugitive')
    call helper#plug('tpope/vim-markdown')
    call helper#plug('tpope/vim-sensible')
    call helper#plug('tpope/vim-unimpaired')
    call helper#plug('tpope/vim-git')
    call helper#plug('tpope/vim-obsession')
    call helper#plug('chrisbra/csv.vim')
    call helper#plug('pboettch/vim-cmake-syntax')
    call helper#plug('vim-scripts/std_c.zip')
    call helper#plug('triglav/vim-visual-increment')
    call helper#plug('jonhiggs/vim-readline')
    call helper#plug('bfrg/vim-cpp-modern')

    if helper#plug('pseewald/vim-anyfold')
        augroup Anyfold
            autocmd!
            autocmd Filetype * AnyFoldActivate
            autocmd Filetype * exec 'norm! zR'
            autocmd Filetype * set foldlevel=99
        augroup END
        let g:anyfold_fold_comments = v:true
    endif

    if helper#plug('noahfrederick/vim-skeleton')
        if has('nvim')
            let g:skeleton_template_dir = $HOME.'/.config/nvim/templates'
        else
            let g:skeleton_template_dir = $HOME.'/.vim/templates'
        endif

    endif

    if helper#plug('junegunn/vim-easy-align')
        nmap ga <Plug>(EasyAlign)
        vmap <Enter> <Plug>(EasyAlign)
    endif " easy-align

    if helper#plug('rhysd/conflict-marker.vim')
        let g:conflict_marker_enable_mappings = 0
        nmap <buffer>]x <Plug>(conflict-marker-next-hunk)
        nmap <buffer>[x <Plug>(conflict-marker-prev-hunk)
    endif " conflict-marker

    if helper#plug('rhysd/git-messenger.vim')
        let g:git_messenger_no_default_mappings = v:true
        let g:git_messenger_include_diff = v:true
        let g:git_messenger_max_popup_height = 8
        noremap <silent><leader>g :GitMessenger<cr>
    endif " git-messenger

    if helper#plug('mattboehm/vim-accordion')
        nnoremap <leader><pageup> :AccordionZoomOut<CR>
        nnoremap <leader><pagedown> :AccordionZoomIn<CR>
        nnoremap <c-w>2 :Accordion 2<CR>
        nnoremap <c-w>3 :Accordion 3<CR>
        nnoremap <c-w>4 :Accordion 4<CR>
        nnoremap <c-w>0 :AccordionStop<CR>
    endif " accordion

    if helper#plug('thaerkh/vim-indentguides')
        if $WSL_DISTRO_NAME ==# ''
            let g:indentguides_spacechar = 'ðŸ®Œ'
            let g:indentguides_tabchar = 'â–‘'
        else
            let g:indentguides_spacechar = '|'
            let g:indentguides_tabchar = '>'
        endif
        let g:indentguides_toggleListMode = v:false
        let g:indentguides_firstlevel = v:false
    endif " indentguides

    " TODO: try neoformat
"    if helper#plug('chiel92/vim-autoformat')
"        let g:formatterpath = [$HOME.'/.local/bin']
"        let g:autoformat_autoindent = 0
"        let g:autoformat_retab = 0
"        let g:formatters_python = ['yapf']
"        let g:formatdef_yapf = '"yapf --style=\"$HOME/.style.yapf\""'
"        noremap <leader>F :Autoformat<CR>
"    endif " autoformat

    if g:ide
        if helper#plug('neoclide/coc.nvim')
            set hidden
            set nobackup nowritebackup
            set updatetime=250
            set shortmess+=c
            inoremap <silent><expr> <Tab>
                        \ pumvisible() ? "\<C-n>" :
                        \ <SID>check_back_space() ? "\<Tab>" :
                        \ coc#refresh()
            inoremap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<C-h>"

            function s:check_back_space() abort
                let col = col('.') - 1
                return !col || getline('.')[col - 1] =~# '\s'
            endfunction

            inoremap <silent><expr> <c-space> coc#refresh()
            if has('patch8.1.1068')
                inoremap <expr> <cr> complete_info()["selected"] != "-1" ? "\<C-y>" : "\<C-g>u\<cr>"
            else
                imap <expr> <cr> pumvisible() ? "\<C-y>" : "\<C-g>u\<cr>"
            endif

            nmap <silent> gd <Plug>(coc-definition)
            nmap <silent> gy <Plug>(coc-type-definition)
            nmap <silent> gi <Plug>(coc-implementation)
            nmap <silent> gr <Plug>(coc-references)
        endif
        call helper#plug('honza/vim-snippets')
        if helper#plug('neomake/neomake')
            func! SetStatusLime()
                let b:statuslime_right = neomake#statusline#get(bufnr('%'), {
                            \ 'use_highlights_with_defaults': 0,
                            \ 'format_running': '{{running_job_names}}',
                            \ 'format_loclist_ok': 'âœ“',
                            \ 'format_loclist_issues': '%s',
                            \ 'format_quickfix_ok': '',
                            \ 'format_quickfix_issues': '%s',
                            \ 'format_loclist_type_W': '{{count}} warning(s) ',
                            \ 'format_loclist_type_E': '{{count}} error(s) ',
                            \ 'format_loclist_type_I': '{{count}} message(s) ',
                            \ })
                let b:statuslime_left = coc#status()
            endfunc
            " Don't report issues inside system headers
            function PostprocessClangTidy(entry)
                try
                    let l:fname = a:entry['filename']
                catch
                    let l:fname = bufname(a:entry['bufnr'])
                endtry
                try
                    if l:fname =~# '/usr/include'
                        let a:entry['valid'] = -1
                    endif
                endtry
            endfunction
            let g:neomake_open_list = 2
            let g:neomake_highlight_columns = 0
            let g:neomake_makers_buffer_output = 0
            let g:neomake_virtualtext_current_error = 0
            let g:neomake_info_sign          = { 'text': 'â–·' }
            let g:neomake_message_sign       = { 'text': 'â–¶' }
            let g:neomake_cpp_enabled_makers = executable('clang++') ?
                        \ ['clangtidy'] : ['gcc']
            let g:neomake_cpp_clangtidy_maker = {
                        \ 'exe': '/home/athresher/.local/share/clang/run-clang-tidy.py',
                        \ 'args': [
                        \       '-quiet',
                        \       '-j', string(g:nproc),
                        \       '-checks=-*,bugprone-*,cppcoreguidelines-*,'
                        \           .'clang-analyzer*,hicpp-*,misc-*,modernize-*,mpi-*,'
                        \           .'openmp-*,performance-*,readability-*,portability-*'
                        \ ],
                        \ 'postprocess': function('PostprocessClangTidy'),
                        \ 'errorformat':
                        \       '%E%f:%l:%c: fatal error: %m,' .
                        \       '%E%f:%l:%c: error: %m,' .
                        \       '%W%f:%l:%c: warning: %m,' .
                        \       '%-G%\m%\%%(LLVM ERROR:%\|No compilation database found%\)%\@!%.%#,' .
                        \       '%E%m'
                        \}
            call add(g:neomake_cpp_enabled_makers, 'cppcheck')

        endif " neomake
    endif

    call helper#end()
endfunction
call g:Plugins()

function! g:Functions()
    " Useful for debugging syntax highlighting
    func! HiAtCur()
        let l:name = ''
        for id in synstack(line('.'), col('.'))
            let l:name = synIDattr(id, 'name')
            echo l:name
        endfor
        if l:name isnot# ''
            exec 'hi '.l:name
        endif
    endfunc

    function! CMakeBuildDir()
        return exists('b:cmake_build_dir') ?
                    \ b:cmake_build_dir :
                    \ (exists('g:cmake_build_dir') ?
                        \ g:cmake_build_dir : 'build')
    endfunction

    function! RunCMakeAndMake(args)
        if filereadable('CMakeLists.txt')
            let l:build = CMakeBuildDir()
            let l:cmd = '!cmake -B '.CMakeBuildDir().' -DCMAKE_EXPORT_COMPILE_COMMANDS=ON -S $(pwd) '.a:args
            silent exec l:cmd
            silent exec 'make -j -C '.CMakeBuildDir()
            redraw!
            echom l:cmd
        else
            silent exec 'make -j'
            redraw!
            echom 'make -j'
        endif
    endfunction

    function! RunMakeClean()
        if filereadable('CMakeLists.txt')
            let l:cmd = 'make -C '.CMakeBuildDir().' clean'
        else
            let l:cmd = 'make clean'
        endif
        silent exec l:cmd
        redraw!
        echom l:cmd
    endfunction
endfunction
call g:Functions()

function! g:Maps()

    nmap <leader>l :copen<cr>
    nmap <silent> <leader>C :call RunMakeClean()<cr>
    nmap <silent> <leader>M :call RunCMakeAndMake('-DCMAKE_CXX_CLANG_TIDY=clang-tidy')<cr>
    nmap <silent> <leader>m :call RunCMakeAndMake('-UCMAKE_CXX_CLANG_TIDY=')<cr>


    " Quick reload of vimrc
    nnoremap <leader>V :source $MYVIMRC<cr>
    " Use ` as optional wincmd prefix
    nnoremap ` <c-w>
    nnoremap <c-w>` `
    nmap <silent><leader>? <Plug>(helper-toggle)
    " Ctrl+K to search for cursor text and show in quickfix
    " why doesn't this work?
    " nnoremap <c-k> :grep! \b<C-R><C-W>\b<CR>:cw<CR>
    nnoremap <leader>h :call HiAtCur()<cr>

endfunction
call g:Maps()

function! g:IDE()
        call neomake#configure#automake({
                    \ 'TextChanged': {},
                    \ 'InsertLeave': {},
                    \ 'BufWritePost': {'delay': 0},
                    \ 'BufWinEnter': {},
                    \}, 100)
        call neomake#signs#RedefineErrorSign({ 'text': 'â•³', 'texthl': 'Error' })
        call neomake#signs#RedefineWarningSign({ 'text': 'âœ—', 'texthl': 'Error' })
endfunction
if g:ide | call g:IDE() | endif
