
set shiftwidth=4
set softtabstop=4
set expandtab

" Plugins {{{

" Keep track of plugins to auto-generate help menu
let g:plugin_master_list = []
let g:plugin_help_list = []
func! s:plug(repo, has_help)
    try
        call plug#(a:repo)
        let g:plugin_master_list = g:plugin_master_list + [a:repo]
        if a:has_help != 0
            let g:plugin_help_list = g:plugin_help_list + [a:repo]
        endif
    catch
        echoerr 'Failed to load plugin "'.a:repo.'"'
    endtry
endfunc
let g:plugin_help_menu = 1

if has('nvim')
    let g:plugin_path = '~/.config/nvim/repos'
else
    let g:plugin_path = '~/.vim/repos'
endif

try
    call plug#begin(g:plugin_path)

    " TODO: look at official yapf plugin
    "call s:plug('mizuchi/stl-syntax')
    "call s:plug('unblevable/quick-scope') " TODO: see if this caused slowdown
    call s:plug('ap/vim-buftabline', 1)
    call s:plug('bfrg/vim-cpp-modern', 0)
    call s:plug('google/vim-searchindex', 1)
    call s:plug('junegunn/vim-easy-align', 1)
    call s:plug('machakann/vim-sandwich', 1)
    call s:plug('mboughaba/i3config.vim', 0)
    call s:plug('peterrincker/vim-argumentative', 1)
    call s:plug('reedes/vim-litecorrect', 0)
    call s:plug('rhysd/git-messenger.vim', 1)
    call s:plug('sheerun/vim-polyglot', 0)
    call s:plug('skywind3000/quickmenu.vim', 0)
    call s:plug('tbastos/vim-lua', 0)
    call s:plug('thinca/vim-ref', 1)
    call s:plug('tpope/vim-abolish', 1)
    call s:plug('tpope/vim-commentary', 1)
    call s:plug('tpope/vim-eunuch', 1)
    call s:plug('tpope/vim-fugitive', 1)
    call s:plug('tpope/vim-rsi', 1)
    call s:plug('tpope/vim-sensible', 0)
    call s:plug('tpope/vim-unimpaired', 1)
    call s:plug('yggdroot/indentline', 1)

    if has('nvim')
        call s:plug('prabirshrestha/async.vim', 0)
        call s:plug('prabirshrestha/vim-lsp', 1)
        call s:plug('prabirshrestha/asyncomplete.vim', 0)
        call s:plug('prabirshrestha/asyncomplete-lsp.vim', 0)
    endif

    call plug#end()
catch
    echom "vim-plug is missing or broken"
endtry

" }}}

" Options {{{
" Disable vi-compatible defaults
set nocompatible
" Allow folding in files using {{{ and }}}
set foldenable
set foldmethod=marker
" Always use UTF-8
set encoding=utf-8
scriptencoding utf-8
" No annoying sounds / flashes
set noerrorbells
" Allow files to customize settings on open
set modeline
" Characters used when list=on
set listchars=eol:$,tab:>\ ,extends:>,precedes:<,nbsp:+,trail:_
" Character used to mark wrapped lines
set showbreak==>\
" Copy the indentation from the previous line
set autoindent
" Short blink when typing matching parens
set showmatch
set matchtime=1
" Allow leaving modified buffers without saving
set hidden
" Try to keep windows similarly sized
set equalalways
set eadirection=both
" Don't show the active mode
set noshowmode
" Search as you're typing
set incsearch
" Allow backspacing over everything
set backspace=indent,eol,start
" Enable the mouse
set mouse=a
" Set US English for spellcheck
set spelllang=en_us
" Always show statusbar
set laststatus=2
" Don't wrap lines by default
set nowrap
" Don't page output
set more
" Default to new windows appearing below current
set splitbelow
" Don't try to set the cursor
set guicursor=
" Preview window is 1 line tall
"set previewheight=1
" CursorHold kicks in after 1/4 second
set updatetime=100
" Show cmd in progress
set showcmd
" Recursive searching
set path+=**
" Tab completion menu
set wildmenu
" TODO: Set this in an autocmd so that popups are unaffected
"try
"" Minimum active window size
"    set winwidth=80
"    set winheight=24
"    " Keep the cursor near the center of the screen
"    set scrolloff=8
"catch
"    set winheight=8
"    set scrolloff=1
"endtry
" Allow windows to collapse completely
set winminwidth=1
set winminheight=1
set completeopt+=menuone
set completeopt+=noinsert

filetype plugin indent on
" TODO: Auto-center the view when cursor would move off screen, like emacs
" }}}

" Functions {{{

function! s:is_loaded(name)
    if empty(filter(split(execute('scriptnames'), "\n"),
        \ 'v:val =~? "'.a:name.'"'))
        return 0
    endif
    return 1
endfunc

" Wrapper for setting highlights
function! s:hi(group, fg, bg, attr)
    let l:cmd = [ 'hi', a:group, 'guifg='.a:fg[0], 'guibg='.a:bg[0],
        \ 'gui='.a:attr, 'ctermfg='.a:fg[1], 'ctermbg='.a:bg[1],
        \ 'cterm='.a:attr ]
    execute join(l:cmd, ' ')
endfunc

let g:statuslime_hover = 'left'
func! SetStatusLime()
    let g:statuslime_left = g:statuslime_hover 
    let g:statuslime_right = 'right'
    return ''
endfunc

" }}}

" Color Palette {{{

" different theme to distinguish vim / neovim
if has('nvim')
    " srcery color palette
    let s:black          = ['#1C1B19', 235] " first 16 xterm colors are gruvbox
    let s:red            = ['#EF2F27', 124]
    let s:green          = ['#519F50', 106]
    let s:yellow         = ['#FBB829', 172]
    let s:blue           = ['#2C78BF', 66]
    let s:magenta        = ['#E02C6D', 132]
    let s:cyan           = ['#0AAEB3', 72]
    let s:white          = ['#D0BFA1', 246]
    let s:bright_black   = ['#918175', 245]
    let s:bright_red     = ['#F75341', 167]
    let s:bright_green   = ['#98BC37', 142]
    let s:bright_yellow  = ['#FED06E', 214]
    let s:bright_blue    = ['#68A8E4', 109]
    let s:bright_magenta = ['#FF5C8F', 175]
    let s:bright_cyan    = ['#53FDE9', 108]
    let s:bright_white   = ['#FCE8C3', 223]

    " xterm colors.
    let s:orange        = ['#FF5F00', 202]
    let s:bright_orange = ['#FF8700', 208]
    let s:hard_black    = ['#121212', 233]
    let s:xgray1        = ['#262626', 235]
    let s:xgray2        = ['#303030', 236]
    let s:xgray3        = ['#3A3A3A', 237]
    let s:xgray4        = ['#444444', 238]
    let s:xgray5        = ['#4E4E4E', 239]
    let s:xgray6        = ['#585858', 240]
else
    " monokai palette
    let s:black   = ['#272822', 234]
    let s:red     = ['#e73c50', 196]
    let s:green   = ['#A6E22D', 148]
    let s:yellow  = ['#E6DB74', 186]
    let s:blue    = ['#66d9ef', 81]
    let s:magenta = ['#ae81ff', 141]
    let s:cyan    = ['#A1EFE4', 153] " verify 256 color
    let s:white   = ['#E8E8E3', 252]
    let s:bright_black   = ['#75715E', 59]
    let s:bright_red     = ['#ff0000', 52]
    let s:bright_green   = ['#9EC400', 76] " verify 256 color
    let s:bright_yellow  = ['#E7C547', 229] " verify 256 color
    let s:bright_blue    = ['#7AA6DA', 147] " verify 256 color
    let s:bright_magenta = ['#B77EE0', 183] " verify 256 color
    let s:bright_cyan    = ['#54CED6', 159] " verify 256 color
    let s:bright_white   = ['#d8d8d3', 250]
    
    let s:orange        = ['#FD9720', 208]
    let s:bright_orange = ['#F92772', 197]
    let s:hard_black    = ['#211F1C', 233]
    let s:xgray1        = ['#2D2E27', 235]
    let s:xgray2        = ['#383a3e', 236]
    let s:xgray3        = ['#3f4145', 237]
    let s:xgray4        = ['#575b61', 237]
    let s:xgray5        = ['#64645e', 239]
    let s:xgray6        = ['#8F908A', 243]
endif

let s:none = ['NONE', 'NONE']

" }}}

" Highlights {{{

call s:hi('Normal', s:bright_white, s:black, 'NONE')

for group in ['Visual', 'VisualNOS', 'Search', 'IncSearch']
    call s:hi(group, s:none, s:none, 'inverse')
endfor

for group in ['NonText', 'SpecialKey']
    call s:hi(group, s:xgray4, s:none, 'NONE')
endfor

call s:hi('MatchParen', s:bright_magenta, s:none, 'bold')
call s:hi('Conceal', s:xgray2, s:none, 'NONE')
call s:hi('StatusLine', s:bright_white, s:xgray1, 'NONE')
call s:hi('VertSplit', s:bright_white, s:none, 'NONE')
call s:hi('WildMenu', s:blue, s:black, 'bold')
call s:hi('ErrorMsg', s:bright_white, s:red, 'NONE')
call s:hi('Directory', s:green, s:none, 'bold')
call s:hi('Title', s:green, s:none, 'bold')
call s:hi('MoreMsg', s:yellow, s:none, 'bold')
call s:hi('Question', s:orange, s:none, 'bold')
call s:hi('Warning', s:red, s:none, 'bold')

call s:hi('Cursor', s:black, s:yellow, 'NONE')
hi! link vCursor Cursor
hi! link iCursor Cursor
hi! link lCursor Cursor

call s:hi('Special', s:orange, s:none, 'NONE')
call s:hi('Comment', s:bright_black, s:none, 'italic')
call s:hi('Todo', s:bright_white, s:black, 'bold,italic')
call s:hi('Error', s:bright_red, s:none, 'italic')
call s:hi('String', s:bright_green, s:none, 'NONE')

for group in ['Constant', 'Character', 'Boolean', 'Number', 'Float']
    call s:hi(group, s:bright_magenta, s:none, 'NONE')
endfor

call s:hi('Type', s:cyan, s:none, 'bold')
call s:hi('StorageClass', s:orange, s:none, 'bold')
call s:hi('Typedef', s:bright_cyan, s:none, 'bold')
call s:hi('Delimiter', s:bright_black, s:none, 'NONE')
for group in ['Statement', 'Conditional', 'Repeat', 'Label', 'Exception', 'Keyword']
    call s:hi(group, s:red, s:none, 'bold')
endfor
call s:hi('Identifier', s:bright_orange, s:none, 'NONE')
call s:hi('PreProc', s:red, s:none, 'NONE')
call s:hi('Include', s:red, s:none, 'NONE')
call s:hi('Define', s:red, s:none, 'NONE')
call s:hi('PreCondit', s:red, s:none, 'NONE')
call s:hi('Structure', s:cyan, s:none, 'bold')

call s:hi('Function', s:yellow, s:none, 'NONE')
call s:hi('Macro', s:red, s:none, 'NONE')


" Make folds blend in so that the status bar and splits are easier to identify
call s:hi('Folded', s:none, s:none, 'italic')
call s:hi('LineNr', s:white, s:xgray1, 'NONE')
call s:hi('CursorLineNr', s:xgray1, s:bright_black, 'NONE')
call s:hi('StatusLineNC', s:xgray3, s:xgray1, 'NONE')

call s:hi('Pmenu', s:bright_white, s:xgray2, 'NONE')
call s:hi('PmenuSel', s:bright_white, s:magenta, 'bold')

call s:hi('DiffDelete', s:red, s:black, 'none')
call s:hi('DiffAdd', s:green, s:black, 'none')
call s:hi('DiffChange', s:cyan, s:black, 'none')
call s:hi('DiffText', s:yellow, s:black, 'none')

if has('spell')
    call s:hi('SpellCap', s:none, s:magenta, 'underline')
    call s:hi('SpellBad', s:none, s:red, 'underline')
    call s:hi('SpellLocal', s:none, s:yellow, 'underline')
    call s:hi('SpellRare', s:none, s:cyan, 'underline')
endif
if has('terminal')
    call s:hi('Terminal', s:bright_white, s:hard_black, 'NONE')
endif

" statuslime colors
call s:hi('LimeNormal',       s:hard_black,    s:white,   'bold')
call s:hi('LimeVisual',       s:hard_black,    s:blue,    'bold')
call s:hi('LimeInsert',       s:hard_black,    s:yellow,  'bold')
call s:hi('LimeReplace',      s:hard_black,    s:orange,  'bold')
call s:hi('LimeTerminal',     s:hard_black,    s:green,   'bold')
call s:hi('LimeCommand',      s:hard_black,    s:magenta, 'bold')
call s:hi('LimeShell',        s:hard_black,    s:cyan,    'bold')
call s:hi('LimeOther',        s:hard_black,    s:red,     'bold')
call s:hi('LimeFile',         s:bright_white,  s:xgray3,  'NONE')
call s:hi('LimeError',        s:bright_red,    s:xgray1,  'bold')
call s:hi('LimeRuler',        s:bright_white,  s:xgray2,  'NONE')
call s:hi('LimeInactiveBar',  s:hard_black,    s:xgray2,  'NONE')
call s:hi('LimeInactiveMode', s:hard_black,    s:xgray3,  'NONE')
call s:hi('LimeLeft',         s:bright_orange, s:xgray1,  'NONE')
call s:hi('LimeRight',        s:bright_yellow, s:xgray1,  'NONE')

" buftabline colors
call s:hi('BufTabLineCurrent', s:hard_black,   s:white,        'bold')
call s:hi('BufTabLineActive',  s:blue,         s:white,        'NONE')
call s:hi('BufTabLineHidden',  s:bright_black, s:white,        'NONE')
call s:hi('BufTabLineFill',    s:black,        s:bright_black, 'NONE')

" vim-lsp colors
call s:hi('LspErrorHighlight',       s:red,    s:none, 'underline')
call s:hi('LspWarningHighlight',     s:yellow, s:none, 'underline')
call s:hi('LspInformationHighlight', s:blue,   s:none, 'underline')
call s:hi('LspHintHighlight',        s:green,  s:none, 'underline')

" vim-lsp semantic highlighting
call s:hi('SemFunction',   s:yellow,         s:none, 'NONE')
call s:hi('SemMethod',     s:bright_yellow,  s:none, 'italic')
call s:hi('SemNamespace',  s:bright_magenta, s:none, 'NONE')
call s:hi('SemEnum',       s:bright_blue,    s:none, 'NONE')
call s:hi('SemClass',      s:bright_yellow,  s:none, 'NONE')
call s:hi('SemType',       s:cyan,           s:none, 'NONE')
call s:hi('SemTemplate',   s:bright_cyan,    s:none, 'NONE')
call s:hi('SemEnumMember', s:magenta,        s:none, 'NONE')
call s:hi('SemVariable',   s:bright_white,   s:none, 'NONE')
call s:hi('SemField',      s:bright_white,   s:none, 'italic')


" }}}

" Maps {{{

if has("terminal")
    " Navigate out of terminal mode more easily
    tnoremap <esc> <c-\><c-n>
    tnoremap <c-w> <c-\><c-n><c-w>
endif
" Quick reload of vimrc
nnoremap <leader><home> :source $MYVIMRC<cr>
" Clear search with <C-L>
nnoremap <c-l> :noh<cr><c-l>

nnoremap ` <c-w>
nnoremap <c-w>` `

nmap <leader>1 <Plug>BufTabLine.Go(1)
nmap <leader>2 <Plug>BufTabLine.Go(2)
nmap <leader>3 <Plug>BufTabLine.Go(3)
nmap <leader>4 <Plug>BufTabLine.Go(4)
nmap <leader>5 <Plug>BufTabLine.Go(5)
nmap <leader>6 <Plug>BufTabLine.Go(6)
nmap <leader>7 <Plug>BufTabLine.Go(7)
nmap <leader>8 <Plug>BufTabLine.Go(8)
nmap <leader>9 <Plug>BufTabLine.Go(9)
nmap <leader>0 <Plug>BufTabLine.Go(10)

noremap <silent><leader>? :call quickmenu#toggle(g:plugin_help_menu)<cr>
noremap <silent><leader>g :GitMessenger<cr>

nmap ga <Plug>(EasyAlign)
vmap <Enter> <Plug>(EasyAlign)

" }}}

" Plugin Configuration {{{

let g:netrw_banner = 0
let g:netrw_liststyle = 3
let g:netrw_list_hide = netrw_gitignore#Hide()

let g:indentLine_char_list = ['|', '¦', '┆', '┊']
let g:indentLine_setColors = 0

let g:buftabline_indicators = 1
let g:buftabline_numbers = 2

let g:git_messenger_include_diff = v:true
let g:git_messenger_max_popup_height = 8

let g:echodoc#enable_at_startup = 1

let g:quickmenu_options = "H"
try
    " Populate quickmenu with installed plugins with helpfiles
    call quickmenu#current(g:plugin_help_menu)
    call quickmenu#reset()
    call quickmenu#header('plugin help')
    for p in g:plugin_help_list
        let short = substitute(
                \ substitute(
                    \ split(p, '/')[1],
                \ 'vim-', '', 'g'),
            \ '.vim', '', 'g')
        call quickmenu#append(short, 'h '.short.'.txt', '')
    endfor
catch
    echom 'quickmenu could not be initialized'
endtry
" }}}


" TODO: Move this to a plugin
" This shades the background of every 5th line to give
" a similar benefit to relative line numbers without
" the visual clutter
" TODO: blank lines are skipped, find a solution
let cidx = 1
for c in [s:xgray1, s:xgray2, s:xgray3, s:xgray4, s:xgray5, s:xgray6]
    call s:hi('LineMark'.cidx, s:none, c, 'NONE')
    let cidx += 1
endfor

func! ClearLineHelpers()
    call clearmatches()
endfunc

func! SetLineHelpers()
    call clearmatches()
    if &ro
        return
    endif
    let l:win = winsaveview()
    let l:interval = 5
    let l:line = line('.') - l:interval
    let l:cidx = 1
    while l:line > l:win['topline']
        call matchaddpos('LineMark'.l:cidx, [l:line])
        let l:line -= l:interval
        if l:cidx < 6
            let l:cidx += 1
        endif
    endwhile
    let l:line = line('.') + l:interval
    let l:height = winheight(0)
    let l:cidx = 1
    while l:line < l:win['topline']+l:height
        call matchaddpos('LineMark'.l:cidx, [l:line])
        let l:line += l:interval
        if l:cidx < 6
            let l:cidx += 1
        endif
    endwhile
endfunc

"au! CursorHold * call SetLineHelpers()
"au! CursorMoved * call ClearLineHelpers()

" For now, only enable LSP on neovim. Will configure for vim8 later.
if has('nvim') == 0
    finish
endif

if executable('clangd')
    au User lsp_setup call lsp#register_server({
        \ 'name': 'clangd',
        \ 'cmd': {server_info->['clangd']},
        \ 'whitelist': ['cpp', 'c'],
        \ 'semantic_highlight': {
        \   'entity.name.function.cpp': 'SemFunction',
        \   'entity.name.function.method.cpp': 'SemMethod',
        \   'entity.name.namespace.cpp': 'SemNamespace',
        \   'entity.name.type.class.cpp': 'SemClass',
        \   'entity.name.type.enum.cpp': 'SemEnum',
        \   'entity.name.type.template.cpp': 'SemTemplate',
        \   'variable.other.cpp': 'SemVariable',
        \   'variable.other.enummember.cpp': 'SemEnumMember',
        \   'variable.other.field.cpp': 'SemField'
        \ }
        \ })
endif

if executable('pyls')
    " pip install python-language-server
    au User lsp_setup call lsp#register_server({
        \ 'name': 'pyls',
        \ 'cmd': {server_info->['pyls']},
        \ 'whitelist': ['python'],
        \ })
endif

let g:lsp_preview_doubletap = 0
let g:lsp_signs_enabled = 1
let g:lsp_virtual_text_enabled = 1

" The preview window does not update while typing / moving cursor
" inside signature help. This function should continue to show
" the help while the arguments are being entered.
function! s:sig()
    " TODO
endfunc

function! s:on_lsp_buffer_enabled() abort
    setlocal omnifunc=lsp#complete
    nmap <buffer> <leader>D <plug>(lsp-peek-definition)
    nmap <buffer> <leader>d <plug>(lsp-peek-declaration)
    nmap <buffer> <leader>R <plug>(lsp-rename)
    nmap <buffer> <leader>s <plug>(lsp-signature-help)
    nmap <buffer> <leader>r <plug>(lsp-references)
    nmap <buffer> <leader>h <plug>(lsp-hover)
endfunction

augroup lsp_install
    au!
    " call s:on_lsp_buffer_enabled only for languages that has the server registered.
    autocmd User lsp_buffer_enabled call s:on_lsp_buffer_enabled()
augroup END
